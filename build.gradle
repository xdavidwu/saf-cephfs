buildscript {
	repositories {
		google()
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:8.10.0'
	}
}
apply plugin: 'com.android.application'

// for lint target
repositories {
	mavenCentral()
}

android {
	namespace 'link.xdavidwu.saf.cephfs'
	compileSdkVersion 'android-36'
	buildToolsVersion '35.0.1'
	ndkPath '/opt/android-ndk'
	ndkVersion '28.2.13676358'

	defaultConfig {
		applicationId 'org.safcephfs'
		minSdkVersion 26
		targetSdkVersion 36
		externalNativeBuild {
			cmake {
				arguments '-DWITH_MANPAGE=OFF', '-DWITH_RDMA=OFF', '-DWITH_KVS=OFF', '-DWITH_FUSE=OFF', '-DWITH_BLUESTORE=OFF', '-DWITH_XFS=OFF', '-DWITH_RBD=OFF', '-DWITH_OPENLDAP=OFF', '-DWITH_RADOSGW=OFF', '-DWITH_LZ4=OFF', '-DWITH_KRBD=OFF', '-DWITH_LTTNG=OFF', '-DWITH_MGR=OFF', '-DWITH_BABELTRACE=OFF', '-DWITH_CEPHFS=OFF', '-DWITH_LIBRADOSSTRIPER=OFF', '-DWITH_TESTS=OFF', '-DWITH_REENTRANT_STRSIGNAL=ON', '-DWITH_SYSTEMD=OFF', '-DWITH_MGR_DASHBOARD_FRONTEND=OFF', '-DWITH_RADOSGW_KAFKA_ENDPOINT=OFF', '-DWITH_RADOSGW_AMQP_ENDPOINT=OFF', '-DWITH_RADOSGW_BEAST_OPENSSL=OFF', '-DDEBUG_GATHER=OFF', '-DWITH_JAEGER=OFF', '-DWITH_CEPHFS_JAVA=ON', '-DCMAKE_FIND_ROOT_PATH=/opt/android-libs/aarch64', '-DOPENSSL_USE_STATIC_LIBS=ON', '-DICU_I18N_LIBRARY_RELEASE=/opt/android-libs/aarch64/lib/libicui18n.a', '-DICU_UC_LIBRARY_RELEASE=/opt/android-libs/aarch64/lib/libicuuc.a', '-DICU_DATA_LIBRARY_RELEASE=/opt/android-libs/aarch64/lib/libicudata.a', '-DWITH_SYSTEM_BOOST=OFF', '-DWITH_LIBCEPHSQLITE=OFF', '-DWITH_RADOSGW_D4N=OFF'
				targets 'cephfs_jni'
				cppFlags '-fPIC', '-Wl,-Bsymbolic'
			}
		}
		ndk {
			// TODO: perhaps x86_64, but android-x86 isn't really active these days
			abiFilters 'arm64-v8a'
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFile getDefaultProguardFile('proguard-android.txt')
		}
	}

	externalNativeBuild {
		cmake {
			path 'src/main/jni/ceph/CMakeLists.txt'
			// agp weirdness: specify version to use system cmake
			// https://developer.android.com/reference/tools/gradle-api/8.1/com/android/build/api/dsl/Cmake#version()
			// TODO: use cmake from android-sdk?
			version '4.0.3'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}
}

allprojects {
	repositories {
		google()
	}
}
